/*
 * Copyright Â© 2017-2020  Kynetics  LLC
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 */

apply plugin: 'com.android.application'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.kynetics.uf.service"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 27
        versionName versionFromGit(project)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def isRunningOnTravis = System.getenv("CI") == "true"

    signingConfigs {
        if (isRunningOnTravis) {
            nxp_platform {
                storePassword System.getenv("uf_store_password")
                keyAlias System.getenv("nxp_alias")
                keyPassword System.getenv("nxp_key_password")
                storeFile file('../travis/uf_keystore.jks')
                v2SigningEnabled true
                v1SigningEnabled true
            }

            aosp_platform {
                storePassword System.getenv("uf_store_password")
                keyAlias System.getenv("aosp_alias")
                keyPassword System.getenv("asop_key_password")
                storeFile file('../travis/uf_keystore.jks')
                v2SigningEnabled true
                v1SigningEnabled true
            }
        } else if(project.properties.containsKey("uf-android-client-service.storePath")){
            debug {
                keyAlias project.properties['uf-android-client-service.keyAlias']
                keyPassword project.properties['uf-android-client-service.keyPassword']
                storeFile file(project.properties['uf-android-client-service.storePath'])
                storePassword project.properties['uf-android-client-service.storePassword']
                v2SigningEnabled true
                v1SigningEnabled true
            }

        }

    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        if (isRunningOnTravis) {
            nxp_platform_key {
                signingConfig signingConfigs.nxp_platform
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                matchingFallbacks = ['release', 'debug']
            }

            aosp_platform_key {
                signingConfig signingConfigs.aosp_platform
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                matchingFallbacks = ['release', 'debug']
            }
        } else {
            debug{
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.debug
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation("com.github.kynetics:uf-ddiclient:v1.0.0"){
        exclude group: "org.slf4j", module: "slf4j-simple"
    }
    implementation 'de.psdev.slf4j-android-logger:slf4j-android-logger:1.0.5'
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "androidx.appcompat:appcompat:1.0.0"
    implementation "androidx.preference:preference:1.0.0"
    testImplementation 'junit:junit:4.12'
//    implementation 'com.github.Kynetics.uf-ddiclient:ddiclient-core:v0.4.7'
    implementation project(':uf-client-service:uf-client-service-api')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'com.google.code.gson:gson:2.8.1'
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation "com.squareup.retrofit2:converter-gson:2.5.0"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation "com.squareup.okhttp3:okhttp:3.9.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.1.1'
    implementation 'androidx.security:security-crypto:1.0.0-rc01'
    androidTestImplementation("androidx.test.ext:junit:1.1.1")
}
